# load antidote
source /opt/homebrew/opt/antidote/share/antidote/antidote.zsh
antidote load

# Enable completion system
autoload -Uz compinit
compinit

# Completion menu style settings
zstyle ':completion:*' menu select
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
zstyle ':completion:*' matcher-list 'm:{a-zA-Z-_}={A-Za-z_-}' 'r:|=*' 'l:|=* r:|=*'

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

### See https://github.com/holman/dotfiles ###
# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# dotfiles
# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

if [ -d "$DOTFILES" ];
then
  # all of our zsh files
  typeset -U config_files
  config_files=($DOTFILES/**/*.zsh)

  # load the path files
  for file in ${(M)config_files:#*/path.zsh}
  do
    source $file
  done

  # load everything but the path and completion files
  for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
  do
    source $file
  done

  # load every completion after autocomplete loads
  for file in ${(M)config_files:#*/completion.zsh}
  do
    source $file
  done

  unset config_files
fi

eval "$(zoxide init zsh)"
if command -v wt >/dev/null 2>&1; then eval "$(command wt config shell init zsh)"; fi
